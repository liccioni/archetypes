plugins {
    id 'com.github.jmongard.git-semver-plugin'
    id 'com.adarshr.test-logger'
}

allprojects {
    group = 'net.liccioni'
    version = semver.version as String
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'maven-publish'

    java {
        sourceCompatibility = "11"
        targetCompatibility = "11"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    testlogger {
        theme 'mocha'
    }

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok
        testImplementation libs.assertj
        testImplementation libs.junit.jupiter
        testImplementation libs.junit.params
        testRuntimeOnly libs.junit.engine
        implementation 'com.github.liccioni.archetypes:party:0.0.38'
    }

    tasks.named('test') {
        // Use JUnit Platform for unit tests.
        useJUnitPlatform()
    }

    java {
        withSourcesJar()
//        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}